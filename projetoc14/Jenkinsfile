def stageStatus = [:]

pipeline {
    agent any

    parameters {
        string(name: 'PROJECT_PATH', defaultValue: 'Ze-dos-Livros/projetoc14', description: 'Caminho do projeto')
        string(name: 'MAVEN_TOOL', defaultValue: 'Maven3', description: 'Ferramenta Maven a usar')
    }

    stages {

        stage('Load Utils'){
            steps{
                script{
                    utils = load 'projetoc14/utils.groovy'
                }
            }
        }

        stage('Build Once') {
            steps {
                echo "Buildando o projeto apenas uma vez"
                dir('projetoc14') {
                    sh "rm -rf target || true"
                    sh "mvn clean install -DskipTests"
                }
            }
            post {
                success { script { stageStatus['Build Once'] = 'SUCCESS' } }
                failure { script { stageStatus['Build Once'] = 'FAILED' } }
            }
        }

        stage('Parallel Tests') {
            parallel {
                stage('Test') {
                    steps {
                        echo "Executando testes unitários..."
                        dir('projetoc14') {
                            sh "mvn -Dtest=**/implementacao/*Test.java test"
                        }
                    }
                    post {
                        always {
                            junit 'projetoc14/target/surefire-reports/*.xml'
                            archiveArtifacts artifacts: 'projetoc14/target/surefire-reports/*', fingerprint: true
                        }
                        success { script { stageStatus['Test'] = 'SUCCESS' } }
                        failure { script { stageStatus['Test'] = 'FAILED' } }
                    }
                }
                stage('Integration Test') {
                    steps {
                        echo "Executando testes de integração..."
                        dir('projetoc14') {
                            sh "mvn -Dtest=**/controller/*Test.java test"
                        }
                    }
                    post {
                        always {
                            dir('projetoc14') {
                                junit 'target/surefire-reports/*.xml'
                                archiveArtifacts artifacts: 'target/surefire-reports/*', fingerprint: true
                            }
                        }
                        success { script { stageStatus['Integration Test'] = 'SUCCESS' } }
                        failure { script { stageStatus['Integration Test'] = 'FAILED' } }
                    }
                }
            }
        }

        stage('Generate Coverage'){
            steps {
                script {
                    try {
                        echo "Gerando relatório de cobertura Jacoco..."
                        dir('projetoc14') {
                            sh "${tool 'Maven3'}/bin/mvn test jacoco:report"
                        }

                        stageStatus['Code Coverage'] = 'SUCCESS'

                        publishHTML([
                            reportDir: "projetoc14/target/site/jacoco",
                            reportFiles: 'index.html',
                            reportName: 'Jacoco Coverage',
                            keepAll: true,
                            alwaysLinkToLastBuild: true
                        ])
                    } catch (Exception e) {
                        echo "Falha ao gerar relatório de cobertura: ${e.message}"
                        stageStatus['Code Coverage'] = 'FAILED'
                    }
                }
            }
        }


        stage('Build') {
            steps {
                echo "Empacotando JAR..."
                dir('projetoc14') {
                    sh "${tool 'Maven3'}/bin/mvn -B package"
                }
            }
            post {
                success {
                    script {
                        dir('projetoc14') {
                            archiveArtifacts artifacts: 'target/*.jar', fingerprint: true
                        }
                        stageStatus['Build'] = 'SUCCESS'
                    }
                }
                failure { script { stageStatus['Build'] = 'FAILED' } }
            }
        }
    }

    post {

        failure {
            script {
                utils.sendStatusEmail("FAILED ❌", stageStatus, this)
            }
        }

        success {
            script {
                utils.sendStatusEmail("SUCCESS ✅", stageStatus, this)
            }
        }
    }
}